/// Btc grammar.
use crate::syntax;
use crate::syntax::*;
use std::str::FromStr;

grammar;

// Macros 


Comma<Rule>: Vec<Rule> = {
    <mut v:(<Rule> ",")*> <e:Rule?> => match e { 
        None => v,
        Some(e) => {
            v.push(e);
            v
        }
    }
};

pub Nat : u32 = {
    <s:r"[0-9]+"> => u32::from_str(s).unwrap(),
}

pub Identifier : syntax::Name = {
    <s: r"_?[a-zA-Z][_a-zA-Z0-9-']*"> => String::from(s),
}

pub TyStructBody : (syntax::Name, syntax::Type) = {
    <name: Identifier> ":" <ty: Type> => (name, ty),
}

pub TermStructBody : (syntax::Name, syntax::Expr) = {
    <name: Identifier> ":" <e: Expr> => (name, e)
}

// No operator precedence for now
pub Operator : syntax::Operator = {
    "+" => Operator::Add, 
    "-" => Operator::Sub, 
    "*" => Operator::Mul, 
    "/" => Operator::Div, 
    ">" => Operator::Gt, 
    "<" => Operator::Lt, 
    "==" => Operator::Eq,
}


pub Type : syntax::Type = {
    "Unit" => Type::Unit,
    "Natural" => Type::Natural, 
    "String" => Type::String, 
    "Bool" => Type::Bool,
    "struct" <name: Identifier>  => Type::Named(name),
    "List" "[" <t: Type> "]" => Type::List(Box::new(t)),
    "(" <tys : Comma<Type>> ")" "->" <result: Type> => Type::Arrow(tys, Box::new(result)),
    // <name : Identifier> "{"  <body: Comma<TyStructBody>> "}" => Type::Struct(name, body),
}


pub Expr : syntax::Expr = {
    <l: @L> <e: ExprOne> <r: @R> => Expr(Loc::new(l, r), e), 
}

pub ExprOne : syntax::ExprOne<syntax::Expr> = {
    "(" <e: ExprOne> ")" => e, 

    "unit" => ExprOne::Unit,

    <id: Identifier> => ExprOne::Variable(id),
    
    <n: Nat> => ExprOne::Natural(n), 

    // Hack for now
    "\"" <s: Identifier> "\"" => ExprOne::String(s),

    "true" => ExprOne::Bool(true), 
    "false" => ExprOne::Bool(false),

    "(" <e: Expr> ":" <ty: Type> ")" => ExprOne::Annotation(Box::new(e), ty),

    "let" <name: Identifier>  "=" <e: Expr> ";" <l: @L> "(" <body: Expr?> ")" <r: @L> => match body {
        None => ExprOne::Let(name, Box::new(e), Box::new(Expr(Loc::new(l, r), ExprOne::Unit))), 
        Some(body) =>  ExprOne::Let(name, Box::new(e), Box::new(body)), 
    },

    "[" <elems: Comma<Expr>> "]" => ExprOne::List(elems), 

    "if" <predicate: Expr> "{" <t: Expr> "}" "else" "{" <f: Expr> "}" => 
        ExprOne::If(Box::new(predicate), Box::new(t), Box::new(f)), 

    "function" "(" <args: Comma<Identifier>> ")" "->" "{" <body: Expr> "}" => { 
        let args = if args.is_empty() { vec![ String::from("unit") ] } else { args };
        ExprOne::Lambda(args, Box::new(body)) 
    },

    <l: @L> <f: Expr> "(" <args: Comma<Expr>> ")" <r: @R>  => {
        let args = if args.is_empty() { vec![ Expr(Loc::new(l, r).generate(), ExprOne::Unit) ] } else { args };
        ExprOne::Application(Box::new(f), args)
    },

    "(" <name: Identifier> "{" <body: Comma<TermStructBody>> "}" ")" => ExprOne::Struct(name, body),

    "(" <a: Expr> <op: Operator> <b: Expr> ")" => ExprOne::Primitive(op, Box::new(a), Box::new(b)),

    <e: Expr> "." <field: Identifier> => ExprOne::StructIndex(Box::new(e), field),
}

pub Return : Type = {
    "->" <ty: Type> => ty,
        => Type::Unit,
}

pub Def : syntax::Def = {
    "struct" <id: Identifier> "{"  <body: Comma<TyStructBody>>  "}" => Def::Struct(id, body), 
    <l: @L> "function" <name: Identifier> "("  <args: Comma<TyStructBody>> ")" <codomain: Return> "{" <body: Expr> "}"<r: @R> => {
        let loc = Loc::new(l, r);
        let (args, tys) : (Vec<_>, Vec<_>)= args.into_iter().unzip();
        let domain = if tys.is_empty() { vec![Type::Unit] } else { tys };
        let args = if args.is_empty() { vec![String::from("unit")] } else { args };
        let f = Type::Arrow(domain, Box::new(codomain)); 
        let lambda = Expr(loc, ExprOne::Lambda(args, Box::new(body)));
        Def::Fun(name, Expr(loc, ExprOne::Annotation(Box::new(lambda), f)))
    }
};

pub Program : Vec<syntax::Def> = {
    Def => vec![<>], 
    <mut p: Program> <d: Def> => {
        p.push(d); 
        p
    }
}